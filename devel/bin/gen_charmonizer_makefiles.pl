#!/usr/bin/perl

# Licensed to the Apache Software Foundation (ASF) under one or more
# contributor license agreements.  See the NOTICE file distributed with
# this work for additional information regarding copyright ownership.
# The ASF licenses this file to You under the Apache License, Version 2.0
# (the "License"); you may not use this file except in compliance with
# the License.  You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

use strict;
use warnings;

package Charmonizer::Build::Makefile;
use File::Find qw();
use FindBin;
use Carp qw( confess );
use Cwd qw( getcwd );

sub new {
    my ( $class, %args ) = @_;

    # Validate args, create object.
    for (qw( dir filename obj_ext )) {
        $args{$_} or confess("Missing required param '$_'");
    }
    my $dir = $args{dir};
    my $self = bless { 
        dir      => $dir,  
        filename => $args{filename},
        obj_ext  => $args{obj_ext},
    }, $class;

    # Gather source paths, normalized for the target OS.
    my $orig_dir = getcwd();
    chdir($dir);
    -d 'src' or confess("Can't find 'src' directory within '$dir'");
    my ( @h_files, @c_files, @c_tests );
    push @c_files, "charmonize.c";
    File::Find::find( {
        wanted => sub {
            if (/\.c$/) {
                if (/^Test/) {
                    push @c_tests, $File::Find::name;
                }
                else {
                    push @c_files, $File::Find::name;
                }
            }
            elsif (/\.h$/) {
                push @h_files, $File::Find::name;
            }
        },
    }, 'src', );
    chdir($orig_dir);
    $self->{c_files} = [ sort $self->pathify(@c_files) ];
    $self->{h_files} = [ sort $self->pathify(@h_files) ];
    $self->{c_tests} = [ sort $self->pathify(@c_tests) ];

    return $self;
}

sub pathify { confess "abstract method" }

sub unixify {
    my $self = shift;
    map { my $copy = $_; $copy =~ tr{\\}{/}; $copy } @_;
}

sub winnify {
    my $self = shift;
    map { my $copy = $_; $copy =~ tr{/}{\\}; $copy } @_;
}

sub objectify {
    my ( $self, @objects ) = @_;
    for (@objects) {
        s/\.c$/$self->{obj_ext}/ or die "No match: $_";
    }
    return @objects;
}

sub test_execs {
    my $self = shift;
    my @test_execs = grep { $_ !~ /Test\.c/ } @_; # skip Test.c entry
    for (@test_execs) {
        s/.*(Test\w+)\.c$/$1\$(EXEEXT)/ or die "no match: $_";
    }
    return @test_execs;
}

sub test_blocks {
    my $self = shift;
    my @c_files = grep { $_ !~ /Test\.c/ } @_; # skip Test.c entry
    my @blocks;
    for my $c_file (@c_files) {
        my $exe = $c_file; 
        $exe =~ s/.*(Test\w+)\.c$/$1\$(EXEEXT)/ or die "no match $exe";
        my ($obj) = $self->objectify($c_file);
        push @blocks, <<END_BLOCK;
$exe: src/Charmonizer/Test$self->{obj_ext} $obj
\t\$(LINKER) \$(LINKFLAGS) src/Charmonizer/Test$self->{obj_ext} $obj \$(LINKOUT)"\$@"
END_BLOCK
    }
    return @blocks;
}

sub clean_target { confess "abstract method" }

sub clean_target_posix {
    qq|clean:\n\trm -f \$(CLEANABLE)|;
}

sub clean_target_win {
    qq|clean:\n\tCMD /c FOR %i IN (\$(CLEANABLE)) DO IF EXIST %i DEL /F %i|;
}

sub gen_makefile {
    my ( $self, %args ) = @_;
    open my $fh, ">", $args{file} or die "open '$args{file}' failed: $!\n";
    my $license = $self->license;
    my $content = <<EOT;
# GENERATED BY $FindBin::Script: do not hand-edit!!!
#
$license
$args{top}
PROGNAME= charmonize\$(EXEEXT)

TESTS= $args{test_execs}

OBJS= $args{objs}

TEST_OBJS= $args{test_objs}

HEADERS= $args{headers}

CLEANABLE= \$(OBJS) \$(PROGNAME) \$(TEST_OBJS) \$(TESTS) *.pdb

all: \$(PROGNAME)

\$(PROGNAME): \$(OBJS)
	\$(LINKER) \$(LINKFLAGS) \$(OBJS) \$(LINKOUT)"\$(PROGNAME)"

\$(OBJS) \$(TEST_OBJS): \$(HEADERS)

tests: \$(TESTS)

$args{test_blocks}
$args{clean_target}

EOT
    print $fh $content;
}

sub write_makefile {
    my $self = shift;
    my @objects      = $self->objectify( @{ $self->{c_files} } );
    my @test_objects = $self->objectify( @{ $self->{c_tests} } );
    my @test_execs   = $self->test_execs( @{ $self->{c_tests} } );
    my @test_blocks  = $self->test_blocks( @{ $self->{c_tests} } );

    $self->gen_makefile(
        test_execs   => join(" ", $self->pathify(@test_execs)),
        objs         => join(" ", $self->pathify(@objects)),
        test_objs    => join(" ", $self->pathify(@test_objects)),
        headers      => join(" ", $self->pathify( @{ $self->{h_files} } ) ),
        test_blocks  => join("\n", $self->pathify(@test_blocks)),
        top          => $self->top,
        clean_target => $self->clean_target,
        file         => $self->{filename},
    );
}

sub license {
    return <<'END_LICENSE';
# Licensed to the Apache Software Foundation (ASF) under one or more
# contributor license agreements.  See the NOTICE file distributed with
# this work for additional information regarding copyright ownership.
# The ASF licenses this file to You under the Apache License, Version 2.0
# (the "License"); you may not use this file except in compliance with
# the License.  You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
END_LICENSE
}

package Charmonizer::Build::Makefile::Posix;
BEGIN { our @ISA = qw( Charmonizer::Build::Makefile ) }

sub new { 
    my $class = shift;
    return $class->SUPER::new(
        filename => 'Makefile', 
        obj_ext  => '.o',
        @_ 
    );
}

sub top {
    return <<END_STUFF;
CC= cc
DEFS=
CFLAGS= -Isrc \$(DEFS)
EXEEXT=
LINKER= \$(CC)
LINKFLAGS= \$(CFLAGS)
LINKOUT= -o

.c.o:
\t\$(CC) \$(CFLAGS) -c \$*.c -o \$@
END_STUFF
}

sub clean_target { shift->clean_target_posix }
sub pathify      { shift->unixify(@_) }

package Charmonizer::Build::Makefile::MSVC;
BEGIN { our @ISA = qw( Charmonizer::Build::Makefile ) }

sub new { 
    my $class = shift;
    return $class->SUPER::new(
        filename => 'Makefile.MSVC', 
        obj_ext  => '.obj',
        @_ 
    );
}


sub top {
    return <<END_STUFF;
CC= cl
DEFS=
CFLAGS= -Isrc -nologo -D_CRT_SECURE_NO_WARNINGS \$(DEFS)
EXEEXT= .exe
LINKER= link
LINKFLAGS= -nologo
LINKOUT= /OUT:

.c.obj:
\t\$(CC) \$(CFLAGS) -c \$< -Fo\$@
END_STUFF
}

sub pathify      { shift->winnify(@_) }
sub clean_target { shift->clean_target_win }

package Charmonizer::Build::Makefile::MinGW;
BEGIN { our @ISA = qw( Charmonizer::Build::Makefile ) }

sub new { 
    my $class = shift;
    return $class->SUPER::new(
        filename => 'Makefile.MinGW', 
        obj_ext  => '.o',
        @_ 
    );
}

sub top {
    return <<END_STUFF;
CC= gcc
DEFS=
CFLAGS= -Isrc \$(DEFS)
EXEEXT= .exe
LINKER= \$(CC)
LINKFLAGS= \$(CFLAGS)
LINKOUT= -o

.c.o:
\t\$(CC) \$(CFLAGS) -c \$*.c -o \$@
END_STUFF
}

sub pathify      { shift->winnify(@_) }
sub clean_target { shift->clean_target_win }

### actual script follows
package main;

my $makefile_posix = Charmonizer::Build::Makefile::Posix->new( dir => '.' );
my $makefile_msvc  = Charmonizer::Build::Makefile::MSVC->new( dir => '.' );
my $makefile_mingw = Charmonizer::Build::Makefile::MinGW->new( dir => '.' );
$makefile_posix->write_makefile;
$makefile_msvc->write_makefile;
$makefile_mingw->write_makefile;

__END__

=head1 NAME

gen_charmonizer_makefiles.pl

=head1 SYNOPSIS

    gen_charmonizer_makefiles.pl - keeps the Makefiles in sync with the live tree.

=head1 DESCRIPTION

Be sure to run this code from the charmonizer subdirectory (where the
existing Makefiles live).

